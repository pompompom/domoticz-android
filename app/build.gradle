apply plugin: 'com.android.application'


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))
        def value = 0
        def runTasks = gradle.startParameter.taskNames
        println "****************************"
        println "runTasks: ${gradle.startParameter.taskNames}"

        if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks || ':app:compilePaidReleaseSources' in runTasks) {
            value = 1;

            println "Build Version: +1"
        }
        println "****************************"

        def versionMajor = 0
        def versionMinor = 1
        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        def version_Code = versionProps['VERSION_CODE'].toInteger() + value

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['VERSION_CODE'] = version_Code.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            versionCode version_Code
            versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
            minSdkVersion 16
            targetSdkVersion 25
            multiDexEnabled true
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    //noinspection GroovyAssignabilityCheck
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }

    productFlavors {
        free {
            applicationId 'nl.hnogames.domoticz'

            resValue "string", "APK_VALIDATE_DEBUG", "$System.env.APK_VALIDATE_DEBUG"
            resValue "string", "APK_VALIDATE_PROD", "$System.env.APK_VALIDATE_PROD"
            resValue "string", "APK_LICENSE_PREMIUM", "$System.env.APK_LICENSE_PREMIUM"

            resValue "string", "analiticsapikey", "$System.env.GOOGLE_ANALITICS_ID"
            resValue "string", "googleapikey", "$System.env.GOOGLEAPIKEY"
            resValue "string", "app_name_domoticz", 'Domoticz Lite'
            buildConfigField "boolean", "LITE_VERSION", "true"
        }
        paid {
            applicationId 'nl.hnogames.domoticz.premium'

            resValue "string", "APK_VALIDATE_DEBUG", "$System.env.APK_VALIDATE_DEBUG"
            resValue "string", "APK_VALIDATE_PROD", "$System.env.APK_VALIDATE_PROD"
            resValue "string", "APK_LICENSE_PREMIUM", "$System.env.APK_LICENSE_PREMIUM"

            resValue "string", "analiticsapikey", "$System.env.GOOGLE_ANALITICS_ID"
            resValue "string", "googleapikey", "$System.env.GOOGLEAPIKEY"
            resValue "string", "app_name_domoticz", 'Domoticz'
            buildConfigField "boolean", "LITE_VERSION", "false"
        }
    }

    afterEvaluate {
        tasks.matching {
            it.name.startsWith('dex')
        }.each { dx ->
            if (dx.additionalParameters == null) {
                dx.additionalParameters = []
            }
            dx.additionalParameters += '--multi-dex'
            dx.additionalParameters += "--main-dex-list=$projectDir/<filename>".toString()
        }
    }

    //noinspection GroovyAssignabilityCheck
    dexOptions {
        preDexLibraries false
    }
}
repositories {
    maven {
        url "https://jitpack.io"
    }
}

/*
 * Copyright (C) 2015 Domoticz - Mark Heinis
 *
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

//noinspection GroovyAssignabilityCheck
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    paidWearApp project(path: ':wear', configuration: 'paidRelease')

    compile 'com.android.support:support-v4:25.3.0'
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:support-v13:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'

    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.code.gson:gson:2.7'

    compile 'com.google.firebase:firebase-core:10.2.0'
    compile 'com.google.firebase:firebase-crash:10.2.0'

    compile 'com.google.android.gms:play-services-analytics:10.2.0'
    compile 'com.google.android.gms:play-services-wearable:10.2.0'
    compile 'com.google.android.gms:play-services-maps:10.2.0'
    compile 'com.google.android.gms:play-services-gcm:10.2.0'
    compile 'com.google.android.gms:play-services-location:10.2.0'
    compile 'com.google.android.gms:play-services-places:10.2.0'

    compile 'com.android.volley:volley:1.0.0'
    compile 'fr.avianey.com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.marvinlabs:android-floatinglabel-widgets:1.6.1@aar'
    compile 'com.larswerkman:lobsterpicker:1.0.1'

    compile 'com.github.zurche:plain-pie:v0.2.2'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'

    compile 'com.github.dexafree:MaterialList:v3.2.2'
    compile 'com.github.hotchemi:android-rate:1.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.nhaarman.listviewanimations:lib-core:3.1.0@aar'
    compile 'eu.inloop:easygcm:1.6.2@aar'
    compile 'me.dm7.barcodescanner:zxing:1.9'
    compile 'com.github.jd-alexander:LikeButton:0.2.0'
    compile 'jp.wasabeef:recyclerview-animators:2.2.4'
    compile 'com.github.javiersantos:PiracyChecker:0.0.3'
    compile 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'

    compile('com.mikepenz:materialdrawer:5.8.2@aar') {
        transitive = true
    }

    compile 'com.schibstedspain.android:leku:3.1.0'

    compile 'com.github.k0shk0sh:PermissionHelper:1.1.0'
    compile 'com.github.zagum:SpeechRecognitionView:1.0.2'
    compile 'com.afollestad:digitus:0.3.0'

    compile project(path: ':materialIntro_library')
    compile project(':libs:MemorizingTrustManager')
    compile project(path: ':SeekArc_library')
    compile project(path: ':domoticzapi')
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

apply plugin: 'com.google.gms.google-services'

